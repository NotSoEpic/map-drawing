plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
}

def generatedResources = project(":common").file("src/generated/resources")
def commonResources = project(":common").file("src/main/resources")

repositories {
    flatDir { dir 'libs' }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    include(modImplementation("foundry.veil:veil-fabric-${minecraft_version}:${veil_version}") {
        exclude group: "maven.modrinth"
        exclude group: "me.fallenbreath"
    })
    compileOnly("maven.modrinth:DistantHorizonsApi:${dh_api_version}")
    // for some reason fabric HATES distant horizons in a dev environment..... just download the jar and put it in run/mods yourself
//    runtimeOnly("maven.modrinth:DistantHorizons:${dh_version}-${minecraft_version}")
    modApi("fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${fcap_version}")
}

loom {
    def aw = project(':common').file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
    }
}

sourceSets.main.resources {
    srcDir generatedResources.absolutePath
}