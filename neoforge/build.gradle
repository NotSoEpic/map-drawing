plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

def generatedResources = project(":common").file("src/generated/resources")
def commonResources = project(":common").file("src/main/resources")

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
            gameDirectory = project.file('runs/client')
        }
        data {
            data()
            gameDirectory = project.file('runs/data')
            programArguments.addAll '--all',
                    '--mod', 'mapwright',
                    '--output', generatedResources.absolutePath,
                    '--existing', commonResources.absolutePath
        }
        server {
            server()
            gameDirectory = project.file('runs/server')
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

dependencies {
    implementation("foundry.veil:veil-neoforge-${minecraft_version}:${veil_version}") {
        exclude group: "maven.modrinth"
        exclude group: "me.fallenbreath"
    }
    compileOnly("maven.modrinth:DistantHorizonsApi:${dh_api_version}")
    runtimeOnly("maven.modrinth:DistantHorizons:${dh_version}-${minecraft_version}")
}

sourceSets.main.resources {
    srcDir generatedResources.absolutePath
}